plugins {
    id 'java-library'

    id 'org.openapi.generator' version '6.6.0'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.1.2'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'

    implementation 'org.openapitools:jackson-databind-nullable:0.2.4'

    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
}

openApiGenerate {
    generatorName = "spring"

    inputSpec = "$projectDir/src/main/resources/api-v1.yaml"
    outputDir = "$buildDir/generated"

    apiPackage = "com.github.siwonpawel.similarity.api.v1"
    modelPackage = "com.github.siwonpawel.similarity.api.v1.model"

    generateApiTests = false
    generateModelTests = false

    configOptions.set([
            useSpringBoot3       : "true",
            interfaceOnly        : "true",
            performBeanValidation: "true",
            skipDefaultInterface : "true",
            useTags              : "true",
    ])

    globalProperties.set([
            models: '',
            apis  : ''
    ])
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}

compileJava {
    dependsOn tasks.openApiGenerate
}
